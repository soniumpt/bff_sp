//Rentable shops & living (SNL - 0 living, 1 shop)
//Scripted for MPZ UO Shard
//by Warman - warman@mail.pt
//v1.2 - Timer had to change to the sign. (SEPT 8)
//v1.3 - Shop only type. Ability to lock items. (OCT 8)
//v1.4 - Bug corrections and refinement. (NOV 15)
//v1.5 - Rentable chests. (DEZ 11)
//v1.6 - Bug corrections. (Jun 18 2003) by Vidar
//v1.7 - Move items to bank if released not if timed out just released - Wren - NOV 30 2003
//v1.8 - Modified by Wren to fix anywhere banking :E
//v1.9 - Modified by Wren to fix multiple people being able to rent off the same sign
//v2.0 - Modified so one rented living/shop space per account, all chars on account can use it





  /////////
 //ITEMS//
/////////
[ITEMDEF m_snl_rent]
NAME	= Rented Shop or Living room
ID	= i_memory
TYPE	= t_eq_script

ON=@CREATE
   ATTR=010
   TIMER=-1


[ITEMDEF i_sign_rental]
  ID=i_sign_brass
  TYPE=t_script
  TDATA2=064
  FLIP=1
  DUPELIST=0bd2

ON=@DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif

   //Is room rented?
    IF (<MORE2> == 0)
        MENU mnu_snl_rental
        RETURN 1
    ENDIF 

   //For living
   IF (<TAG.SNL> == 0)
      IF ( (<MORE2> == <SRC.UID>) || (<SRC.ISGM>))
         MENU mnu_snl_lrented
      ENDIF
      RETURN 1

   //For Shop
   ELSE
     SRC.ACT=<TAG.VENDOR>
     IF (<SRC.ISGM>)
       MENU mnu_snl_srented
       return 1
     ELSEIF (<MORE2> == <SRC.UID>)
       IF (0<TAG.ACCOUNT>=0)
          IF ( <SRC.ACT.ISMYPET> )
             TAG.ACCOUNT = <SRC.ACCOUNT>
          ENDIF
       ENDIF
       IF !( <SRC.ACT.ISMYPET> )
          IF strmatch("<TAG.ACCOUNT>","<SRC.ACCOUNT>")
             SRC.NEWITEM=i_memory
             SRC.ACT.COLOR=memory_ipet
             SRC.ACT.LINK=<SRC.UID>
             SRC.ACT.LAYER=layer_special
             SRC.ACT.CONT=<TAG.VENDOR>
             MENU mnu_snl_srented
             return 1
          ENDIF          
       ENDIF
       MENU mnu_snl_srented
       return 1
     ELSE
       IF !(<SRC.ACT.ISMYPET>)
          IF strmatch("<TAG.ACCOUNT>","<SRC.ACCOUNT>")
             SRC.NEWITEM=i_memory
             SRC.ACT.COLOR=memory_ipet
             SRC.ACT.LINK=<SRC.UID>
             SRC.ACT.LAYER=layer_special
             SRC.ACT.CONT=<TAG.VENDOR>
             MENU mnu_snl_srented
             return 1
          ENDIF
       ELSE
          MENU mnu_snl_srented
          return 1
       ENDIF
     ENDIF
   ENDIF

   LOCAL.MSG	= <tag.daysleft>
   if ( <LOCAL.MSG> == 0 )
      LOCAL.MSG	= <eval <TIMER>> minutes
   else
      LOCAL.MSG	= <tag.daysleft> days
   endif
   SRC.SYSMESSAGE This structure is already occupied by <SRC.UID.<MORE2>.name>, paid for <LOCAL.MSG>.
   return 1


ON = @CREATE
     ATTR = 010
     NAME = FOR RENT
     COLOR = 0298

ON=@TIMER
       IF (<TAG.DAYSLEFT> >= 1 )
            TIMER = 86400
            TAG.DAYSLEFT = <eval (<tag.daysleft> - 1)>
            RETURN 1
       ENDIF
   //On lease termination
     //Check for door open
       IF (<LINK.TYPE> = t_door_snl_open)
          LINK.TYPE = t_door_argh_open
       ENDIF

     //Case of shop rental
       IF (<TAG.SNL> == 1)
         //Reset sign acording to Orientation (0 - ew, 1 - ns)
           IF (0<TAG.ORI> == 00)
               DISPID=i_sign_brass
           ENDIF
           IF (0<TAG.ORI> == 01)
               DISPID=i_sign_brass_2
           ENDIF
         TAG.SNL   = 0

         TRY UID.<TAG.VENDOR>.REMOVE
         TAG.VENDOR = 0
         TRY UID.<TAG.LOCK1>.REMOVE
         TAG.LOCK1 = 0
         TRY UID.<TAG.LOCK2>.REMOVE
         TAG.LOCK2 = 0
         TRY UID.<TAG.LOCK3>.REMOVE
         TAG.LOCK3 = 0
         TAG.LOCKS = 0   
       ENDIF

     TIMER = -1
     TRY UID.<MORE2>.SYSMESSAGE Your lease at <REGION.NAME> has just expired.
     TRY UID.<TAG.PLAYERMEM>.REMOVE
     MORE2	= 0              //Reset owner
     NAME      = FOR RENT     //Reset name
     COLOR     = 0298

     RETURN 1

ON=@TARGON_ITEM
   SERV.LOG <SRC.ACCOUNT.NAME>:<SRC.NAME>:<SRC.UID> is trying to lock <SRC.TARG.NAME>:<SRC.TARG.UID>
   IF ((<SRC.TARG.TOPOBJ.ISNPC>) || (<SRC.TARG.TOPOBJ.ISPLAYER>))
      SRC.SYSMESSAGE You can't lock a living person or their items!
      SERV.LOG <SRC.ACCOUNT.NAME>:<SRC.NAME>:<SRC.UID> is trying to lock <SRC.TARG.NAME>:<SRC.TARG.UID>
      RETURN 1
   ELSEIF ((<TAG.LOCK1> == <SRC.TARG.UID>)||(<TAG.LOCK2> == <SRC.TARG.UID>)||(<TAG.LOCK3> == <SRC.TARG.UID>))
//Decreasing numer of locks
        TAG.LOCKS=(<TAG.LOCKS> -1)

      //Cleaning lock flags
        IF (<TAG.LOCK1> == <SRC.TARG.UID>)
            TAG.LOCK1 = <TAG.LOCK2>
            TAG.LOCK2 = <TAG.LOCK3>
            TAG.LOCK3 = 0
        ENDIF
        IF (<TAG.LOCK2> == <SRC.TARG.UID>)
           TAG.LOCK2 = <TAG.LOCK3>
           TAG.LOCK3 = 0
        ENDIF
        IF (<TAG.LOCK3> == <SRC.TARG.UID>)
           TAG.LOCK3 = 0
        ENDIF      
      SRC.TARG.ATTR = 04008
      SRC.TARG.BOUNCE
      RETURN 1
   ENDIF
   IF (<TAG.LOCKS> == 3)
       SRC.SYSMESSAGE You can't lock any more items.
       RETURN 1
   ENDIF

   //Check distance
     LOCAL.POSITION = <SRC.P>
     SRC.P = <P>
     IF !(<SRC.TARG.DISTANCE> < 4)
        SRC.SYSMESSAGE <TARG.DISTANCE> That placement is too far.
        SRC.P = <LOCAL.POSITION>
        RETURN 1
     ENDIF
     SRC.P = <LOCAL.POSITION>

   //Disable exploits
     IF (<SRC.TARG.ATTR> == 010)|(<SRC.TARG.TYPE> == t_corpse)|(<SRC.TARG.ATTR> == 00))
         SRC.SYSMESSAGE You cant lock that down
         RETURN 1
     ENDIF

   IF (<TAG.LOCKS> == 2)
       TAG.LOCKS = 3
       TAG.LOCK3 = <SRC.TARG.UID>
   ENDIF
   IF (<TAG.LOCKS> == 1)
       TAG.LOCKS = 2
       TAG.LOCK2 = <SRC.TARG.UID>
   ENDIF
   IF (<TAG.LOCKS> == 0)
       TAG.LOCKS = 1
       TAG.LOCK1 = <SRC.TARG.UID>
   ENDIF
   SRC.TARG.ATTR = 010
   SRC.TARG.TIMER = -1
   SRC.SYSMESSAGE Item has been locked down.
   RETURN 1

ON=@TARGON_CHAR
   SRC.SYSMESSAGE @40 Clunk
   RETURN 1





  /////////
 //TYPES//
/////////
[TYPEDEF t_door_snl]
ON = @DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
     //is it rented to player?
       IF (<LINK.MORE2> == <SRC.UID>)
          TYPE = t_door
          USE
          TYPE = t_door_snl_open
          TIMER = -1
          RETURN 1
       ENDIF
     SRC.SYSMESSAGE Door doesnt budge.
     RETURN 1


[TYPEDEF t_door_snl_open]
ON = @DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
     //is it rented to player?
       IF (<LINK.MORE2> == <SRC.UID>)
          TYPE = t_door
          USE
          TYPE = t_door_snl
          TIMER = -1
          RETURN 1
       ENDIF
     SRC.SYSMESSAGE Door doesnt budge.
     RETURN 1


[TYPEDEF t_door_argh]
ON = @DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
     TYPE = t_door
     USE
     TYPE = t_door_argh_open
     TIMER = -1
     RETURN 1


[TYPEDEF t_door_argh_open]
ON = @DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
     TYPE = t_door
     USE
     TYPE = t_door_argh
     TIMER = -1
     RETURN 1




  /////////
 //MENUS//
/////////
[MENU mnu_snl_rental]
Rent structure (<REGION.NAME>)		// <menu name>

ON = 0 Rent the room for living (<eval <TAG.PRICE>>gp)	// 0 <menu text>
   //Check for other living areas or shops
   IF ( <SRC.ACCOUNT.CHAR.0.FINDID( m_snl_rent ).uid> )
      SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
      RETURN 1
   ENDIF

   IF (<SRC.ACCOUNT.CHARS>>1)
      IF ( <SRC.ACCOUNT.CHAR.1.FINDID( m_snl_rent ).uid> )
         SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
         RETURN 1
      ENDIF
      IF (<SRC.ACCOUNT.CHARS>>2)
         IF ( <SRC.ACCOUNT.CHAR.2.FINDID( m_snl_rent ).uid> )
            SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
            RETURN 1
         ENDIF
         IF (<SRC.ACCOUNT.CHARS>>3)
            IF ( <SRC.ACCOUNT.CHAR.3.FINDID( m_snl_rent ).uid> )
               SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
               RETURN 1
            ENDIF
            IF (<SRC.ACCOUNT.CHARS>>4)
               IF ( <SRC.ACCOUNT.CHAR.4.FINDID( m_snl_rent ).uid> )
                  SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
                  RETURN 1
               ENDIF
            ENDIF
        ENDIF
     ENDIF
  ENDIF

  //Check money
    IF ( !<SRC.RESTEST <eval <TAG.PRICE>> i_gold> )
       SRC.SYSMESSAGE You dont have enough money on you.
       RETURN 1
    ENDIF

  //Check if someone has already clicked this button and rented
    IF !(<MORE2> == 0)
       SRC.SYSMESSAGE This space has already been rented.
       RETURN 1
    ENDIF

  //Process rental
    SRC.SYSMESSAGE You rent the room at <REGION.NAME>.
    SRC.SYSMESSAGE WARNING: DOOR DOES NOT AUTO CLOSE.
    MORE2		= <SRC.UID>	// link sign to player
    TAG.SNL       	= 0             // living tag
    SRC.NEWITEM		= m_snl_rent	// add memory item
    TAG.PLAYERMEM 	= <SRC.ACT.UID>
    SRC.ACT.LINK	= <UID>		// link memory to sign
    SRC.ACT.EQUIP
    TIMER		= 86400	// 1 days
    TAG.DAYSLEFT        = 4

    SRC.CONSUME <eval (<TAG.PRICE>)> i_gold
  //Case of lazy GM or door open from previous rents
    IF (<LINK.TYPE> == t_door_argh_open)
       LINK.TYPE = t_door
       LINK.USE
       LINK.TIMER = -1
    ENDIF
    LINK.TYPE = t_door_snl
  //Show new status
    NAME = Occupied
    COLOR = 028

ON = 0 Rent the room for shop (<eval <TAG.PRICE>*(5)>gp)	// 0 <menu text>
   IF ( <SRC.ACCOUNT.CHAR.0.FINDID( m_snl_rent ).uid> )
      SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
      RETURN 1
   ENDIF

   IF (<SRC.ACCOUNT.CHARS>>1)
      IF ( <SRC.ACCOUNT.CHAR.1.FINDID( m_snl_rent ).uid> )
         SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
         RETURN 1
      ENDIF
      IF (<SRC.ACCOUNT.CHARS>>2)
         IF ( <SRC.ACCOUNT.CHAR.2.FINDID( m_snl_rent ).uid> )
            SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
            RETURN 1
         ENDIF
         IF (<SRC.ACCOUNT.CHARS>>3)
            IF ( <SRC.ACCOUNT.CHAR.3.FINDID( m_snl_rent ).uid> )
               SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
               RETURN 1
            ENDIF
            IF (<SRC.ACCOUNT.CHARS>>4)
               IF ( <SRC.ACCOUNT.CHAR.4.FINDID( m_snl_rent ).uid> )
                  SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
                  RETURN 1
               ENDIF
            ENDIF
        ENDIF
     ENDIF
  ENDIF

  //Check money
    IF ( !<SRC.RESTEST <eval <TAG.PRICE>*(5)> i_gold> )
       SRC.SYSMESSAGE You dont have enough money on you.
       RETURN 1
    ENDIF

  //Check if someone has already clicked this button and rented
    IF !(<MORE2> == 0)
       SRC.SYSMESSAGE This space has already been rented.
       RETURN 1
    ENDIF

  //Process rental
    SRC.SYSMESSAGE You rent the shop at <REGION.NAME>.
    MORE2		= <SRC.UID>	// link sign to player
    TAG.SNL     	= 1         	// shop tag
    SRC.NEWITEM		= m_snl_rent	// add memory item
    TAG.PLAYERMEM 	= <SRC.ACT.UID>
    SRC.ACT.LINK	= <UID>		// link memory to sign
    SRC.ACT.EQUIP
  //Set timer and consume gold
    TIMER		= 86400	// 1 days
    TAG.DAYSLEFT        = 4
    SRC.CONSUME <eval (<TAG.PRICE>)*(5)> i_gold

  //Case of lazy GM or door open from previous rents
    IF (<LINK.TYPE> == t_door_argh_open)
       LINK.TYPE = t_door
       LINK.USE
       LINK.TIMER = -1
    ENDIF
    LINK.TYPE = t_door_argh

  //Show new status
    NAME = <SRC.NAME>'s shop
    COLOR = 00
    TAG.ACCOUNT = <SRC.ACCOUNT>

  //Setup vendor
    SRC.NEWNPC { C_H_PLAYER_VENDOR 1 C_H_PLAYER_VENDOR_F 1 }
    SRC.ACT.TAG.VENDOR_COLOR	= 010
    SRC.ACT.TAG.VENDOR_TITLE	= player vendor
    TAG.VENDOR = <SRC.ACT.UID> // keep the vendors uid for later use.
    SRC.ACT.FINDLAYER(layer_bankbox).MORE1	= 300
    SRC.ACT.FOOD = 32000
    SRC.ACT.HOME = <MOREP>
    SRC.ACT.GO <MOREP>

  //Add memory item to vendor
    SRC.NEWITEM=i_memory
    SRC.ACT.COLOR=memory_ipet
    SRC.ACT.LINK=<SRC.UID>
    SRC.ACT.LAYER=layer_special
    SRC.ACT.CONT=<TAG.VENDOR>
    RETURN 1

[MENU mnu_snl_lrented]
Rented room at <REGION.NAME>,  <eval (<tag.daysleft>)> days left	// <menu name>

ON = 0 Extend rent time by 24 hours 
     //check if has house
       IF ( <SRC.MEMORYFINDTYPE(memory_guard).uid> )
          SRC.SYSMESSAGE Lease extension refused. You may not have a house and a rented room.
          RETURN 1
       ENDIF

     //check if they have another room
     LOCAL.ROOMCOUNT = 0
     IF ( <SRC.ACCOUNT.CHAR.0.FINDID( m_snl_rent ).uid> )
        LOCAL.ROOMCOUNT =<EVAL (<LOCAL.ROOMCOUNT>+1)>
     ENDIF
     IF (<SRC.ACCOUNT.CHARS>>1)
        IF ( <SRC.ACCOUNT.CHAR.1.FINDID( m_snl_rent ).uid> )
           LOCAL.ROOMCOUNT =<EVAL (<LOCAL.ROOMCOUNT>+1)>
        ENDIF
        IF (<SRC.ACCOUNT.CHARS>>2)
           IF ( <SRC.ACCOUNT.CHAR.2.FINDID( m_snl_rent ).uid> )
              LOCAL.ROOMCOUNT =<EVAL (<LOCAL.ROOMCOUNT>+1)>
           ENDIF
           IF (<SRC.ACCOUNT.CHARS>>3)
              IF ( <SRC.ACCOUNT.CHAR.3.FINDID( m_snl_rent ).uid> )
                 LOCAL.ROOMCOUNT =<EVAL (<LOCAL.ROOMCOUNT>+1)>
              ENDIF
              IF (<SRC.ACCOUNT.CHARS>>4)
                 IF ( <SRC.ACCOUNT.CHAR.4.FINDID( m_snl_rent ).uid> )
                    LOCAL.ROOMCOUNT =<EVAL (<LOCAL.ROOMCOUNT>+1)>
                 ENDIF
              ENDIF
           ENDIF
        ENDIF
     ENDIF

     IF (<LOCAL.ROOMCOUNT>>1)
        SRC.SYSMESSAGE Lease extension refused.  You may only rent one room/shop per account.
        RETURN 1
     ENDIF

     //check if reached time limit
       IF (<TAG.DAYSLEFT> > 4 )
            SRC.SYSMESSAGE Lease cannot be prolongued any further
            RETURN 1
       ENDIF

     //check if has enough money
       IF ( !<SRC.RESTEST <eval ((<TAG.PRICE>)/5)> i_gold> )
          SRC.SYSMESSAGE You dont have enough money on you.
          RETURN 1
       ENDIF

     //Process extension
       SRC.CONSUME <eval (<TAG.PRICE>)/(5)> i_gold
       TAG.DAYSLEFT	= <eval (<TAG.DAYSLEFT> + 1)>
       SRC.SYSMESSAGE Room rent extended.

ON = 0 Free the room
     //Reset room
       MORE2 = 0            //Reset owner
       NAME  = FOR RENT     //Reset name
       COLOR = 0298         //Reset colour

     //Check for door open
       IF (<LINK.TYPE> = t_door_snl_open)
          LINK.TYPE = t_door
          LINK.USE
          LINK.TYPE = t_door_snl
          LINK.TIMER = -1
       ENDIF

     //Process release 
      TRY UID.<TAG.PLAYERMEM>.REMOVE
      SRC.SYSMESSAGE Room is now free

[MENU mnu_snl_srented]
Rented shop at <REGION.NAME>,  <eval (<tag.daysleft>)> days left	// <menu name>

ON = 0 Extend rent time by 24 hours (<eval <TAG.PRICE>>gp)
     //check if they have another room
     VAR.ROOMCOUNT = 0
     IF ( <SRC.ACCOUNT.CHAR.0.FINDID( m_snl_rent ).uid> )
        VAR.ROOMCOUNT =<EVAL (<VAR.ROOMCOUNT>+1)>
     ENDIF
     IF (<SRC.ACCOUNT.CHARS>>1)
        IF ( <SRC.ACCOUNT.CHAR.1.FINDID( m_snl_rent ).uid> )
           VAR.ROOMCOUNT =<EVAL (<VAR.ROOMCOUNT>+1)>
        ENDIF
        IF (<SRC.ACCOUNT.CHARS>>2)
           IF ( <SRC.ACCOUNT.CHAR.2.FINDID( m_snl_rent ).uid> )
              VAR.ROOMCOUNT =<EVAL (<VAR.ROOMCOUNT>+1)>
           ENDIF
           IF (<SRC.ACCOUNT.CHARS>>3)
              IF ( <SRC.ACCOUNT.CHAR.3.FINDID( m_snl_rent ).uid> )
                 VAR.ROOMCOUNT =<EVAL (<VAR.ROOMCOUNT>+1)>
              ENDIF
              IF (<SRC.ACCOUNT.CHARS>>4)
                 IF ( <SRC.ACCOUNT.CHAR.4.FINDID( m_snl_rent ).uid> )
                    VAR.ROOMCOUNT =<EVAL (<VAR.ROOMCOUNT>+1)>
                 ENDIF
              ENDIF
           ENDIF
        ENDIF
     ENDIF

     IF (<VAR.ROOMCOUNT>>1)
        SRC.SYSMESSAGE Lease extension refused.  You may only rent one room/shop per account.
        VAR.ROOMCOUNT=
        RETURN 1
     ENDIF

     //check if reached time limit
       IF (<TAG.DAYSLEFT> > 4 )
            SRC.SYSMESSAGE Lease cannot be prolongued any further
            RETURN 1
       ENDIF

     //check if has enough money
       IF ( !<SRC.RESTEST <eval ((<TAG.PRICE>))> i_gold> )
          SRC.SYSMESSAGE You dont have enough money on you.
          RETURN 1
       ENDIF

     //Process extension
       SRC.CONSUME <eval (<TAG.PRICE>)> i_gold
       TAG.DAYSLEFT	= <eval (<TAG.DAYSLEFT> + 1)>
serv.log rent
     //Extend vendor
       SRC.ACT = <TAG.VENDOR>
       SRC.ACT.FINDLAYER(layer_bankbox).MORE1 = <eval <SRC.ACT.FINDLAYER(layer_bankbox).MORE1>+(50))>
     SRC.SYSMESSAGE Shop rent extended.
     VAR.ROOMCOUNT=

ON = 0 Access comercial menu
     DIALOG d_menu_shop

ON = 0 Lock/Unlock Item. (<EVAL (3 - <TAG.LOCKS>>) Remaining
     TARGET
     //SRC.SYSMESSAGE Temporarily Disabled
	 
ON = 0 Vendor Commands
      //Commands 	 
     DIALOG d_menu_shop2
	  
ON = 0 Free the room
      //Process release 
      TRY UID.<TAG.PLAYERMEM>.REMOVE

      //Reset room
      MORE2     = 0            //Reset owner
      NAME      = FOR RENT     //Reset name
      COLOR     = 0298         //Reset colour
      TAG.SNL   = 0            //Reset shop tag

      //Sign reset acording to Orientation (0 - ew, 1 - ns)
      IF (<EVAL <TAG.ORI>> == 0)
         DISPID=i_sign_brass
         TRY UID.<SRC.UID>.RESEND
      ENDIF
      IF (<EVAL <TAG.ORI>> == 1)
         DISPID=i_sign_brass_2
         TRY UID.<SRC.UID>.RESEND
      ENDIF

      //Check for door
      IF (<LINK.TYPE> = t_door_argh_open)
         LINK.USE
         TIMER = -1
      ENDIF
      LINK.TYPE = t_door_snl

      //Reference Vendor      
      SRC.ACT=<TAG.VENDOR>
      
      //Put Gold in the Bank
      IF !(0<SRC.ACT.FINDLAYER.LAYER_BANKBOX.MORE1>=0)
         SRC.ACT.TAG.GOLD=0<SRC.ACT.FINDLAYER(layer_bankbox)MORE1>
         SRC.ACT.NEWITEM i_gold
         SRC.ACT.ACT.AMOUNT=<SRC.ACT.TAG.GOLD>
         SRC.ACT.ACT.CONT=<SRC.FINDLAYER(layer_bankbox).UID>
      ENDIF      
 
      //Move Stock to bank
      SRC.stock_to_bank

      //Try moving locked items to bank
      IF !(<EVAL <TAG.LOCK1>>=0)
         SRC.ACT=<TAG.LOCK1>
         SRC.ACT.ATTR = 04008
         SRC.ACT.CONT=<SRC.FINDLAYER(layer_bankbox).UID>
         TAG.LOCK1 = 0
      ENDIF

      IF !(<EVAL <TAG.LOCK2>>=0)
         SRC.ACT=<TAG.LOCK2>
         SRC.ACT.ATTR = 04008
         SRC.ACT.CONT=<SRC.FINDLAYER(layer_bankbox).UID>
         TAG.LOCK2 = 0
      ENDIF

      IF !(<EVAL <TAG.LOCK2>>=0)
         SRC.ACT=<TAG.LOCK3>
         SRC.ACT.ATTR = 04008
         SRC.ACT.CONT=<SRC.FINDLAYER(layer_bankbox).UID>
         TAG.LOCK3 = 0
      ENDIF

      TAG.LOCKS = 0

      //Delete vendor
      TRYP 0 UID.<TAG.VENDOR>.REMOVE
      TAG.VENDOR = 0
      TAG.DAYSLEFT = 0
      SRC.SYSMESSAGE Shop is now free
      RETURN 1

        
	  
[MENU mnu_snl_srented_signs_ew]
Select shop Sign
ON=i_sign_brass Brass Sign
   DISPID=i_sign_brass
   TRY UID.<SRC.UID>.RESEND
ON=0bbf Armourer
   DISPID 0bbf
   TRY UID.<SRC.UID>.RESEND
ON=0ba3 Bakery
   DISPID=0ba3
   TRY UID.<SRC.UID>.RESEND
ON=0bc7 Blacksmith
   DISPID=0bc7
   TRY UID.<SRC.UID>.RESEND
ON=0bcd Bowyer
   DISPID=0bcd
   TRY UID.<SRC.UID>.RESEND
ON=0bbd Fletcher
   DISPID=0bbd
   TRY UID.<SRC.UID>.RESEND
ON=0bc1 Jeweler
   DISPID=0bc1
   TRY UID.<SRC.UID>.RESEND
ON=0bad Mage
   DISPID=0bad
   TRY UID.<SRC.UID>.RESEND
ON=0bcb Provisioner
   DISPID=0bcb
   TRY UID.<SRC.UID>.RESEND
ON=0bc5 Reagent Shop
   DISPID=0bc5
   TRY UID.<SRC.UID>.RESEND
ON=0ba5 Tailor
   DISPID=0ba5
   TRY UID.<SRC.UID>.RESEND
ON=0ba7 Tinker
   DISPID=0ba7
   TRY UID.<SRC.UID>.RESEND
ON=0baf Woodworker
   DISPID=0baf
   TRY UID.<SRC.UID>.RESEND




[MENU mnu_snl_srented_signs_ns]
Select shop Sign
ON=i_sign_brass Brass Sign
   DISPID=i_sign_brass_2
   TRY UID.<SRC.UID>.RESEND
ON=0bbf Armourer
   DISPID 0bc0
   TRY UID.<SRC.UID>.RESEND
ON=0ba3 Bakery
   DISPID=0ba4
   TRY UID.<SRC.UID>.RESEND
ON=0bc7 Blacksmith
   DISPID=0bc8
   TRY UID.<SRC.UID>.RESEND
ON=0bcd Bowyer
   DISPID=0bce
   TRY UID.<SRC.UID>.RESEND
ON=0bbd Fletcher
   DISPID=0bbe
   TRY UID.<SRC.UID>.RESEND
ON=0bc1 Jeweler
   DISPID=0bc2
   TRY UID.<SRC.UID>.RESEND
ON=0bad Mage
   DISPID=0bae
   TRY UID.<SRC.UID>.RESEND
ON=0bcb Provisioner
   DISPID=0bcc
   TRY UID.<SRC.UID>.RESEND
ON=0bc5 Reagent Shop
   DISPID=0bc6
   TRY UID.<SRC.UID>.RESEND
ON=0ba5 Tailor
   DISPID=0ba6
   TRY UID.<SRC.UID>.RESEND
ON=0ba7 Tinker
   DISPID=0ba8
   TRY UID.<SRC.UID>.RESEND
ON=0baf Woodworker
   DISPID=0bb0
   TRY UID.<SRC.UID>.RESEND

  //////////////////
 //SPECIAL DIALOG//
//////////////////

[dialog d_menu_shop]
361,256
resizepic 0 0 5120 180 124
text 30 3 455 0
button 7 34 2362 2361 1 0 1 // Go
text 24 30 455 1
button 7 54 2362 2361 1 0 2 // Summon
text 24 50 455 2
button 7 74 2362 2361 1 0 3 // SummonCage
text 24 70 455 3
button 7 94 2362 2361 1 0 4 // Jail
text 24 90 455 4


[dialog d_menu_shop text]
Comercial Menu
Change shop name
Change sign type
Change vendor's title
Access Bank Account

[dialog d_menu_shop button]
on=1
     INPDLG NAME 15
on=2
     //Orientation (0 - ew, 1 - ns)
       IF (<DISPID> == 0bd2)
          TAG.ORI=1
       ELSEIF (<DISPID> == i_sign_brass)
          TAG.ORI=0
       ENDIF
       IF (0<TAG.ORI> == 0)
           MENU mnu_snl_srented_signs_ew
       ENDIF
       IF (0<TAG.ORI> == 1)
           MENU mnu_snl_srented_signs_ns
       ENDIF
on=3
     SRC.ACT=<TAG.VENDOR>
     INPDLG SRC.ACT.TAG.VENDOR_TITLE 15
on=4
   SRC.ACT=<TAG.VENDOR>
   IF ( <SRC.ACT.DISTANCE> > 10 ) 
      SRC.SYSMESSAGE You'll have to get closer to <src.act.name> for that.
      return 1
   ENDIF 
   SRC.BANK



[dialog d_menu_shop2]
361,256
resizepic 0 0 5120 550 255
text 30 3 455 0
text 24 30 455 1
text 24 50 455 2
text 24 70 455 3
text 24 90 455 4
text 24 110 455 5
text 24 130 455 6
text 24 150 455 7
text 24 170 455 8
text 24 190 455 9
text 24 210 455 10
text 24 230 455 11



[dialog d_menu_shop2 text]
                      Vendor Commands

To use these commands just type them to your Vendor.
 
Open - Opens the vendor to allow trading.
Close - Closes the vendor to forbid trading
Stock - Opens the vendor stock to add and remove items for sale.
Price X - Allows you to price any items inside your stock at any desired price.
Status - Tells you how many items and how much gold your vendor currently holds.
Cash - Withdraws all the earned coins currently in your vendor.
 
To change the vendors name just click and drag your vendor and change in the bar. 



  ////////
 //INNS//
////////

[TYPEDEF t_DOOR_INN_open]
ON = @DClick
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
     TYPE=t_door
     USE
     TYPE=t_door_INN
     return 1
     
[TYPEDEF t_DOOR_INN]
ON = @DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
   
   //Is this the owner account?
   IF STRCMP("<UID.<MORE1>.ACCOUNT>","<SRC.ACCOUNT>") = 0
     MENU mnu_inn_rented
     return 1     
   ENDIF

   // Already rented?
   IF ( <MORE1> != 0 )
      SRC.SYSMESSAGE The room is already taken and the door is locked.
      return 1
   ENDIF

   //check if has other room
   IF ( <SRC.FINDID( m_inn_rent ).uid> )
      SRC.SYSMESSAGE You are not allowed more than one rented room.
      RETURN 1
   ENDIF
     
   //check if has house
   IF ( <SRC.MEMORYFINDTYPE(memory_guard).uid> )
      SRC.SYSMESSAGE House owners are not allowed to rent rooms.
      RETURN 1
   ENDIF

   MENU mnu_inn_rent
   RETURN 1   


[MENU mnu_inn_rent]
Rent <NAME> (<REGION.NAME>)		// <menu name>

ON = 0 Rent the room (<eval <MORE2>>gp) for 1 week. // 0 <menu text>
  IF ( !<SRC.RESTEST <eval <MORE2>> i_gold> )
     SRC.SYSMESSAGE You dont have enough money on you.
     RETURN 1
  ENDIF
  SRC.SYSMESSAGE You rent the room at <REGION.NAME>.
  MORE1		= <SRC.UID>		// link door to player
  SRC.NEWITEM	= m_inn_rent		// add memory item
  SRC.ACT.LINK	= <UID>			// link memory to the door
  SRC.ACT.TIMER	= 7*24*60*60		// 7 days
  SRC.ACT.EQUIP
  SRC.CONSUME <eval (<MORE2>)> i_gold
  COLOR = 028

[MENU mnu_inn_rented]
Rented room at <REGION.NAME>,  <eval (<SRC.FINDID( m_inn_rent ).timer>/(60*60))> hours left	// <menu name>

ON = 0 Extend rent time by 24 hours
     IF (<SRC.FINDID.m_inn_rent.timer> > <eval(60*60*24*7)>)
          SRC.SYSMESSAGE Lease cannot be prolongued any further
          RETURN 1
     ENDIF
     IF ( !<SRC.RESTEST <eval ((<MORE2>)/7)> i_gold> )
        SRC.SYSMESSAGE You dont have enough money on you.
        RETURN 1
     ENDIF
     SRC.CONSUME <eval ((<MORE2>)/7)> i_gold
     SRC.FINDID.m_inn_rent.timer = <eval (<SRC.FINDID( m_inn_rent ).timer> + (24*60*60))>
     SRC.SYSMESSAGE Room rent extended.

ON = 0 Free the room
  MORE1		= 0
  COLOR = 0298         //Reset colour
  SRC.FINDID.m_inn_rent..remove
  SRC.SYSMESSAGE Room is now free.
   
ON = 0 Open door
   TYPE = t_door
   USE
   TYPE = t_door_INN_open



[ITEMDEF m_inn_rent]
NAME	= rented inn room
ID	= i_memory
TYPE	= t_eq_script

ON=@TIMER
   TIMER		= 7*24*60*60	// 7 days
   RETURN 1
   
   // On lease termination
   CONT.SYSMESSAGE Your lease of a room at <LINK.REGION.NAME> has just expired.
   LINK.MORE1	= 0
   REMOVE
   return 1





  ////////////////////
 //SHOP TYPE RENTAL//
////////////////////

[ITEMDEF i_sign_rental_s]
  ID=i_sign_brass
  TYPE=t_script
  TDATA2=064
  FLIP=1
  DUPELIST=0bd2

ON = @CREATE
     ATTR = 010
     NAME = FOR RENT
     COLOR = 0298

ON=@DCLICK 
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
  if ( ( <topobj> == <SRC.UID> ) && <SRC.ISGM> )
              src.sysmessage morep set to <src.p>
              MOREP = <src.p>
   endif
   //Is shop/stand rented?
     IF (<MORE2> == 0)
        MENU mnu_s_rental
        RETURN 1
     ENDIF 


   //If this is my account's vendor let me use it with all my characters also let gms use it
     SRC.ACT=<TAG.VENDOR>
     IF (<SRC.ISGM>)
        MENU mnu_snl_srented
        return 1
     ELSEIF (<MORE2> == <SRC.UID>)
       IF (0<TAG.ACCOUNT>=0)
          IF ( <SRC.ACT.ISMYPET> )
             TAG.ACCOUNT = <SRC.ACCOUNT>
          ENDIF
       ENDIF
       IF !( <SRC.ACT.ISMYPET> )
          IF strmatch("<TAG.ACCOUNT>","<SRC.ACCOUNT>")
             SRC.NEWITEM=i_memory
             SRC.ACT.COLOR=memory_ipet
             SRC.ACT.LINK=<SRC.UID>
             SRC.ACT.LAYER=layer_special
             SRC.ACT.CONT=<TAG.VENDOR>
             MENU mnu_snl_srented
             return 1
          ENDIF          
       ENDIF
       MENU mnu_snl_srented
       return 1
     ELSE
       IF !(<SRC.ACT.ISMYPET>)
          IF strmatch("<TAG.ACCOUNT>","<SRC.ACCOUNT>")
             SRC.NEWITEM=i_memory
             SRC.ACT.COLOR=memory_ipet
             SRC.ACT.LINK=<SRC.UID>
             SRC.ACT.LAYER=layer_special
             SRC.ACT.CONT=<TAG.VENDOR>
             MENU mnu_snl_srented
             return 1
          ENDIF
       ELSE
          MENU mnu_snl_srented
          return 1
       ENDIF
     ENDIF

   if ( (0<SRC.TAG.SNL>)  && <SRC.ISGM> )
      if ( <TAG.PRICE> < <SRC.TAG.SNL> )
         src.sysmessage Shop daily price changed from <eval <tag.price>> to <src.tag.snl>.
         return 1
      endif
   endif

    LOCAL.MSG	= <TAG.DAYSLEFT>
   if ( <LOCAL.MSG> == 0 )
      LOCAL.MSG	= <eval <TIMER>/3600> hours
   else
      LOCAL.MSG	= <eval <LOCAL.MSG>> days
   endif
   SRC.SYSMESSAGE This structure is already occupied by <SRC.UID.<MORE2>.name>, paid for <LOCAL.MSG>.
   
   return 1

ON=@TIMER
       IF (<TAG.DAYSLEFT> >= 1 )
            TIMER = 86400
            TAG.DAYSLEFT = <eval (<tag.daysleft> - 1)>
            RETURN 1
       ENDIF

   //On lease termination
     //IF DOOR EXISTS, Check for door open
       IF !(<LINK> == 04ffffffff)
         IF (<LINK.TYPE> = t_door_snl_open)
            LINK.TYPE = t_door_argh_open
         ENDIF
       endif
     TRY UID.<MORE2>.SYSMESSAGE Your lease at <REGION.NAME> has just expired.
     TRY UID.<TAG.PLAYERMEM>.REMOVE
     MORE2	= 0              //Reset owner
     NAME      = FOR RENT     //Reset name
     COLOR     = 0298
         //Reset sign acording to Orientation (0 - ew, 1 - ns)
           IF (<TAG.ORI> == 0)
               DISPID=i_sign_brass
           ENDIF
           IF (<TAG.ORI> == 1)
               DISPID=i_sign_brass_2
           ENDIF
         TRY UID.<TAG.VENDOR>.REMOVE
         TAG.VENDOR = 0
         TRY UID.<TAG.LOCK1>.REMOVE
         TAG.LOCK1 = 0
         TRY UID.<TAG.LOCK2>.REMOVE
         TAG.LOCK2 = 0
         TRY UID.<TAG.LOCK3>.REMOVE
         TAG.LOCK3 = 0
         TAG.LOCKS = 0
     TIMER = -1
     RETURN 1

ON=@TARGON_ITEM
   SERV.LOG <SRC.ACCOUNT.NAME>:<SRC.NAME>:<SRC.UID> is trying to lock <SRC.TARG.NAME>:<SRC.TARG.UID>
   IF ((<SRC.TARG.TOPOBJ.ISNPC>) || (<SRC.TARG.TOPOBJ.ISPLAYER>))
      SRC.SYSMESSAGE You can't lock a living person or their items!
      SERV.LOG <SRC.ACCOUNT.NAME>:<SRC.NAME>:<SRC.UID> is trying to lock <SRC.TARG.NAME>:<SRC.TARG.UID>
      RETURN 1
   ELSEIF ((<TAG0.LOCK1> == <SRC.TARG.UID>)||(<TAG0.LOCK2> == <SRC.TARG.UID>)||(<TAG0.LOCK3> == <SRC.TARG.UID>))
      //Decreasing numer of locks
        TAG.LOCKS=(<TAG0.LOCKS> -1)
      //Cleaning lock flags
        IF (<TAG.LOCK1> == <SRC.TARG.UID>)
            TAG.LOCK1 = <TAG.LOCK2>
            TAG.LOCK2 = <TAG.LOCK3>
            TAG.LOCK3 = 0
        ENDIF
        IF (<TAG.LOCK2> == <SRC.TARG.UID>)
           TAG.LOCK2 = <TAG.LOCK3>
           TAG.LOCK3 = 0
        ENDIF
        IF (<TAG.LOCK3> == <SRC.TARG.UID>)
           TAG.LOCK3 = 0
        ENDIF      
      SRC.TARG.ATTR = 04008
      SRC.TARG.BOUNCE
      RETURN 1
   ENDIF
   IF (<TAG.LOCKS> == 3)
       SRC.SYSMESSAGE You can't lock any more items.
       RETURN 1
   ENDIF

   //Check distance
     LOCAL.POSITION = <SRC.P>
     SRC.P = <P>
     IF !(<SRC.TARG.DISTANCE> < 4)
        SRC.SYSMESSAGE <TARG.DISTANCE> That placement is too far.
        SRC.P = <LOCAL.POSITION>
        RETURN 1
     ENDIF
     SRC.P = <LOCAL.POSITION>

   //Disable exploits
     IF (<SRC.TARG.ATTR> == 010)|(<SRC.TARG.TYPE> == t_corpse)|(<SRC.TARG.ATTR> == 00))
         SRC.SYSMESSAGE You cant lock that down
         RETURN 1
     ENDIF

   IF (<TAG.LOCKS> == 2)
       TAG.LOCKS = 3
       TAG.LOCK3 = <SRC.TARG.UID>
   ENDIF
   IF (<TAG.LOCKS> == 1)
       TAG.LOCKS = 2
       TAG.LOCK2 = <SRC.TARG.UID>
   ENDIF
   IF (<TAG.LOCKS> == 0)
       TAG.LOCKS = 1
       TAG.LOCK1 = <SRC.TARG.UID>
   ENDIF
   SRC.TARG.ATTR = 010
   SRC.TARG.TIMER = -1
   SRC.SYSMESSAGE Item has been locked down.
   RETURN 1



[MENU mnu_s_rental]
Rent structure (<REGION.NAME>)		// <menu name>

ON = 0 Rent the room for shop (<eval <TAG.PRICE>*(5)>gp)	// 0 <menu text>
  //check if has other room

   IF ( <SRC.ACCOUNT.CHAR.0.FINDID( m_snl_rent ).uid> )
      SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
      RETURN 1
   ENDIF

   IF (<SRC.ACCOUNT.CHARS>>1)
      IF ( <SRC.ACCOUNT.CHAR.1.FINDID( m_snl_rent ).uid> )
         SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
         RETURN 1
      ENDIF
      IF (<SRC.ACCOUNT.CHARS>>2)
         IF ( <SRC.ACCOUNT.CHAR.2.FINDID( m_snl_rent ).uid> )
            SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
            RETURN 1
         ENDIF
         IF (<SRC.ACCOUNT.CHARS>>3)
            IF ( <SRC.ACCOUNT.CHAR.3.FINDID( m_snl_rent ).uid> )
               SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
              RETURN 1
            ENDIF
            IF (<SRC.ACCOUNT.CHARS>>4)
               IF ( <SRC.ACCOUNT.CHAR.4.FINDID( m_snl_rent ).uid> )
                  SRC.SYSMESSAGE You are not allowed more than one rented room/shop.
                  RETURN 1
               ENDIF
            ENDIF
        ENDIF
    ENDIF
  ENDIF

  //Check money
    IF ( !<SRC.RESTEST <eval <TAG.PRICE>*(5)> i_gold> )
       SRC.SYSMESSAGE You dont have enough money on you.
       RETURN 1
    ENDIF

  //Check if someone has already clicked this button and rented
    IF !(<MORE2> == 0)
       SRC.SYSMESSAGE This space has already been rented.
       RETURN 1
    ENDIF

  //Process rental
    SRC.SYSMESSAGE You rent the shop at <REGION.NAME>.
    MORE2		= <SRC.UID>	// link sign to player
    SRC.NEWITEM		= m_snl_rent	// add memory item
    TAG.PLAYERMEM 	= <SRC.ACT.UID>
    SRC.ACT.LINK	= <UID>		// link memory to sign
    SRC.ACT.EQUIP

    //Set timer and consume gold
    TIMER		= 86400	// 1 days
    TAG.DAYSLEFT        = 4
    SRC.CONSUME <eval (<TAG.PRICE>)*(5)> i_gold

  //Case of lazy GM or door open from previous rents
    IF !(<LINK> == 04ffffffff)
      IF (<LINK.TYPE> == t_door_argh_open)
         LINK.TYPE = t_door
         LINK.USE
         LINK.TIMER = -1
      ENDIF
      LINK.TYPE = t_door_argh
    ENDIF

  //Show new status
    NAME = <SRC.NAME>'s shop
    COLOR = 00
    TAG.ACCOUNT = <SRC.ACCOUNT>

  //Setup vendor
    SRC.NEWNPC { C_H_PLAYER_VENDOR 1 C_H_PLAYER_VENDOR_F 1 }
    SRC.ACT.TAG.VENDOR_COLOR	= 010
    SRC.ACT.TAG.VENDOR_TITLE	= player vendor
    TAG.VENDOR = <SRC.ACT.UID> // keep the vendors uid for later use.
    SRC.ACT.FINDLAYER(layer_bankbox).MORE1	= 300
    SRC.ACT.FOOD = 32000
    SRC.ACT.HOME = <MOREP>
    SRC.ACT.GO <MOREP>

  //Add memory item to vendor
    SRC.NEWITEM=i_memory
    SRC.ACT.COLOR=memory_ipet
    SRC.ACT.LINK=<SRC.UID>
    SRC.ACT.LAYER=layer_special
    SRC.ACT.CONT=<TAG.VENDOR>
    RETURN 1











  ///////////////////
 //Rentable Chests//
///////////////////

//Type//
//MOREX	-	Price
//LINK	-	Serial of owner

[TYPEDEF t_container_rent]
ON = @DCLICK
   if ( <SRC.ISDEAD> )
      SRC.SYSMESSAGE Get a life...
      return 1
   endif
      IF ((<LINK> == <SRC.UID>) || (<SRC.ISGM>) || (strmatch("<TAG.CHESTOWNER>","<LINK.ACCOUNT>") )
         MENU mnu_chest_manage
         RETURN 1
      ENDIF
      IF (<LINK> == 04fffffff)
         MENU mnu_chest_rental
         RETURN 1
      ENDIF
      SRC.SYSMESSAGE This chest does not belong to you.
      RETURN 1

ON = @TIMER
       IF (<TAG.DAYSLEFT> >= 1 )
            TIMER = 86400
            TAG.DAYSLEFT = <eval (<tag.daysleft> - 1)
            RETURN 1
       ENDIF
     
     TRY UID.<LINK>.SYSMESSAGE Your lease of <name> at <REGION.NAME> has just expired.
     LINK.FINDID.m_chest_rent.remove
     LINK = 04fffffff		//Reset owner
	 TAG.CHESTOWNER=
     COLOR     = 0298
     TIMER = -1
     RETURN 1

//Menus//

[MENU mnu_chest_rental]
Rent chest in (<REGION.NAME>)		// <menu name>

ON = 0 Rent the chest for 5 days at (<eval <MOREX>*(5)>gp)	// 0 <menu text>
   //check if has other chest
     IF ( <SRC.FINDID( m_chest_rent ).uid> )
        SRC.SYSMESSAGE You are not allowed more than one chest rental.
        RETURN 1
     ENDIF
  //Check money
    IF ( !<SRC.RESTEST <eval <MOREX>*(5)> i_gold> )
       SRC.SYSMESSAGE You dont have enough money on you.
       RETURN 1
    ENDIF
  //Process rental
    SRC.SYSMESSAGE You rent the chest at <REGION.NAME>.
    LINK		= <SRC.UID>		// link chest to player
	TAG.CHESTOWNER=<SRC.ACCOUNT>
    SRC.NEWITEM	= m_chest_rent	// add memory item
    MORE1		= <SRC.ACT.UID>	// UID of player memory
    SRC.ACT.LINK	= <UID>		// link memory to sign
    SRC.ACT.EQUIP
    TIMER		= 86400	// 1 days
    TAG.DAYSLEFT        = 4
    SRC.CONSUME <eval (<MOREX>)*(5)> i_gold
  //Show new status
    COLOR = 028
    RETURN 1

[MENU mnu_chest_manage]
Rented chest at <REGION.NAME>,  <eval (<timer>/(60*60))> hours left	// <menu name>

ON = 0 Open chest
     //Open the chest.
	TYPE = T_CONTAINER
	USE
	TYPE = T_CONTAINER_RENT
	RETURN 1

ON = 0 Extend rent time by 24 hours
     //check if reached time limit
       IF (<TAG.DAYSLEFT> > 4)
            SRC.SYSMESSAGE Lease cannot be prolongued any further
            RETURN 1
       ENDIF
     //check if has enough money
       IF ( !<SRC.RESTEST <eval ((<MOREX>)/5)> i_gold> )
          SRC.SYSMESSAGE You dont have enough money on you.
          RETURN 1
       ENDIF
     //Process extension
       SRC.CONSUME <eval (<MOREX>)/(5)> i_gold
       TAG.DAYSLEFT	= <eval (<TAG.DAYSLEFT> + 1)>
       SRC.SYSMESSAGE Room rent extended.

ON = 0 Free the chest
     //Reset room
       LINK = 04fffffff      //Reset owner
       COLOR = 0298         //Reset colour
	   TAG.CHESTOWNER=
     //Process release
       SRC.FINDID.m_chest_rent.remove
       SRC.SYSMESSAGE chest is now free

//Memory item

[ITEMDEF m_chest_rent]
NAME	= Rented chest
ID	= i_memory
TYPE	= t_eq_script

ON=@CREATE
   ATTR=010
   TIMER=-1

//NPC Vendor Items to Bank of Owner
[FUNCTION stock_to_bank]
IF (0<SRC.ACT.FINDLAYER(layer_vendor_stock).RESCOUNT>=0)
   RETURN 1
ENDIF

SRC.ACT.FINDLAYER(layer_vendor_stock).FINDCONT(0).CONT=<SRC.FINDLAYER(layer_bankbox).UID>
stock_to_bank
RETURN 1



[EOF]