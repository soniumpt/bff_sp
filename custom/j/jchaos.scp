//---------------------------Chaos Alignment Script--------------------------
//-									    -
//------------------------By James "Wigifer" Timbrell------------------------
//-                                                                         -
//---------------------------------------------------------------------------
// 17/7/03 Tiff - Fixed the sys message for recruiting if you have below 75% tactics
// 20/3/04 Wren - Changed so previous members cound rejoin and changed some ORDERSAY/DKSAY to CHAOSSAY
// 18/08/18 - Wigifer - Removed references to e_all_npc 

[FUNCTION CHAOS_REGION ]
   if ( 0 )
   elseif ( !strcmpi( "<REGION.NAME>", "Buccaneer's Den" ) )
      return 1
   else
      return 0
   endif


[FUNCTION CHAOSSAY]
   SRC.SYSMESSAGE @90 "<args>"

[EVENTS e_chaos_recruiter]
ON=@GetHit
   SAY By the powers of chaos I shall grant thee death!
   SRC.KILL
   return 1

ON=@SpellEffect
   SAY Your magic is not welcome.
   SRC.SPELLEFFECT s_lightning 1000
   return 1


[DIALOG d_chaos_recruiter]
100, 100

PAGE 0
  resizepic  0 0 5120 400 420
  text 20 20 50 0
  text 20 340 50 1

  button 140 375 <eval bt_cancel_up> <eval bt_cancel_dn> 1 0 0
  button 220 375 <eval bt_ok_up> <eval bt_ok_dn> 1 0 1

  htmlgump 20 60 360 260 2 1 1


[DIALOG d_chaos_recruiter TEXT]
Joining the Chaos Alignment
Are you sure you want to walk the path of chaos?
To learn more about <VAR.lt>B<var.gt>Chaos<var.lt>/B<var.gt>, visit their section on <var.lt>A href="uo.burstfire.net"<var.gt>The Burstfire UO Site<var.lt>/A<var.gt>. <VAR.BREAK><VAR.BREAK>You should know that this is a choice for life - once you commit to the ways of Chaos you won't be able to join any other alignment in the game (with this character).

[DIALOG d_chaos_recruiter BUTTON]
ON = 0
ON = 1
   SRC.ACT.CHAOSSAY So be it! You now walk the path of Chaos!
   SRC.TAG.ALIGNMENT=alignment_chaos
   CHAOSSAY Wear your shield with pride <SRC.SEX Brother\Sister>.
   SRC.NEWITEM i_shield_chaos_2
   SRC.ACT.BOUNCE
   IF !<SRC.FINDID.i_alignment_timer_check>
      SRC.NEWITEM i_alignment_timer_check
      SRC.ACT.CONT <SRC.UID> 
   ENDIF
   return 1


[SPEECH spk_chaos_recruiter]
ON=hi
ON=hello
ON=hail
ON=greeting
ON=greetings
   if ( <SRC.ISALIGNMENT_CHAOS> )
      dorand 2
         CHAOSSAY Greetings.
         CHAOSSAY Hail.
      enddo
   else
      dorand 2
         CHAOSSAY Greetings. I take care of those who wish to JOIN our cause.
         CHAOSSAY Hail. I take care of those who wish to JOIN our cause.
      enddo
   endif

ON=*shield*
   IF (<SRC.CHAOS_RANK>)
      CHAOSSAY Wear your shield with pride.
      SRC.NEWITEM i_shield_chaos_2
      SRC.ACT.BOUNCE
      RETURN 1
   ELSE
      CHAOSSAY How dare thee request that which thou may not touch!
      RETURN 1
   ENDIF

ON=*join*
   if ( ( <SRC.TACTICS> < 75.0 ) && ( <SRC.MAGERY> < 75.0 ) )
      CHAOSSAY You do not have enough knowledge of Tactics or Magery to be of any use.
      return 1
   elseif ((<EVAL <SRC.TAG.WASORDER>> = 1) || (<SRC.TAG.ALIGNMENT>=alignment_order))
      CHAOSSAY It is order you've chosen, there's no turning back!
      return 1  
   elseif (<SRC.TAG.ALIGNMENT>=alignment_chaos)
      CHAOSSAY Aren't we eager! You are already a member of Chaos!
      return 1  
   elseif (<EVAL <SRC.TAG.WASCHAOS>> = 1)
      CHAOSSAY Welcome back to our cause my friend.
      SRC.TAG.ALIGNMENT = alignment_chaos
      SRC.TAG.RECRUITER = <UID>
      IF !<SRC.FINDID.i_alignment_timer_check>
         SRC.NEWITEM i_alignment_timer_check
         SRC.ACT.CONT <SRC.UID> 
      ENDIF
      return 1
   else
      SRC.TAG.RECRUITER = <UID>
      SRC.HTMLDIALOG d_chaos_recruiter
      return 1
   endif

ON=*resign*
   if ( !<SRC.TAG.ALIGNMENT> == alignment_chaos )
      CHAOSSAY You have to be a member to resign.
      return 1
   elseif (<SRC.FINDID.i_alignment_timer_check.UID>)
      CHAOSSAY You have not served us long enough to leave...
      return 1
   else
      SRC.TAG.ALIGNMENT = alignment_none
      SRC.TAG.RECRUITER
      SRC.TAG.WASCHAOS = 01
      CHAOSSAY You are no longer a member of our fine society. Be on your way now.
      return 1
   endif
ON=rank
   if ( <SRC.ISDEAD> )
      EMOTE I can help only the living.
      return 1
   endif

   if !( <SRC.ISALIGNMENT_CHAOS> )
      SAY Stop wasting my time.
      return 1
   endif

   if ( 0<SRC.TAG.CHAOS.GOT> == 0 )
      SRC.TAG.CHAOS.GOT	= 0
   endif

   if ( <SRC.TAG.CHAOS.GOT> >= <SRC.CHAOS_RANK> )
      CHAOSSAY You already received equipment for this rank.
      return 1
   endif

   if ( !(<SRC.CHAOS_RANK> < 7)  && (<SRC.TAG.CHAOS.GOT> < 7) )
      SRC.NEWITEM i_shield_chaos_3
      SRC.ACT.BOUNCE
      CHAOSSAY There you go.
   ELSE
      CHAOSSAY You're not worthy of a reward yet...
   ENDIF

   SRC.TAG.CHAOS.GOT	= <SRC.CHAOS_RANK>
   return 1

[CHARDEF c_chaos_recruiter]
ID=C_MAN
NAME=Fou-Lu
SOUND=snd_HUMAN_M_DIE_01
DAM=50,70
ARMOR=90
AVERSIONS=t_TRAP,t_eerie_stuff
TSPEECH = spk_chaos_recruiter
TEVENTS	= e_chaos_recruiter

  
CATEGORY	= Order and Chaos
SUBSECTION	= Chaos
DESCRIPTION	= Fou-Lu the Recruiter


ON=@Create
  TAG.NAME.SUFFIX	= ", the Recruitment Officer"
  TAG.ALIGNMENT.RANK	= 6
  TAG.ALIGNMENT	= alignment_chaos

  INVUL 1
  COLOR		= colors_skin
  STR		= {100 150}
  DEX		= {80 95}
  INT		= {150 165}
  NPC		= brain_monster
  SWORDSMANSHIP	= {60.0 70.0}
  WRESTLING	= {67.0 90.0}
  MAGICRESISTANCE={65.0 88.0}
  TACTICS	= {60.0 80.0}
  POISONING	= {90.0 100.0}
  ANATOMY       = {40.0 50.0}
  FAME		= 3000
     KARMA	= -100

  ITEMNEWBIE=i_hair_long
  COLOR=color_o_mytheril

  ITEMNEWBIE=i_platemail_chest_shadow
  ITEMNEWBIE=i_platemail_arms_shadow
  ITEMNEWBIE=i_platemail_gloves_shadow
  ITEMNEWBIE=i_platemail_leggings_shadow
  ITEMNEWBIE=i_shield_chaos_2
  ITEMNEWBIE=i_sword_long_verminard
  ITEMNEWBIE=i_CAPE
  COLOR=0af6
  ITEMNEWBIE=i_SASH
  COLOR=0af6




[ITEMDEF i_robe_alignment_chaos]
ID	= i_deathshroud
NAME	= Chaos Elite Robe
TYPE	= T_CLOTHING
ARMOR	= 5
DYE	= 0
VALUE	= 500000

ON = @Create
  HITPOINTS	= 150
  COLOR		= 0af6
  ATTR          = 05

ON = @Equip
   IF (<COLOR>=color_o_shadow)
      COLOR = 0af6
   ELSEIF (<COLOR> = 0af7)
      COLOR = 0af6
   ENDIF

   if ( <CONT.findlayer.layer_hair.uid> )
      CONT.findlayer.layer_hair.ATTR = (<CONT.findlayer.layer_hair.ATTR> | attr_invis)
      CONT.findlayer.layer_hair.REMOVEFROMVIEW
   endif
   IF ( <SRC.TAG.ALIGNMENT>=alignment_chaos)
      IF ( <SRC.TAG0.REP.ALIGNMENT> >= 5000)
         CHAOSSAY You pull on the robe and pull the hood over your head...
      ELSE
         UNEQUIP
         CHAOSSAY You need <eval 5000> order points to wear this!
         RETURN 1
      ENDIF
   ELSE
      UNEQUIP
      SRC.SYSMESSAGE You are not a member of Chaos!
      RETURN 1
   ENDIF

ON = @Unequip
   if ( <CONT.findlayer.layer_hair.uid> )
      CONT.findlayer.layer_hair.ATTR = (<CONT.findlayer.layer_hair.ATTR> & ~ attr_invis)
      CONT.findlayer.layer_hair.UPDATE
   endif
   
[ITEMDEF i_resurrect_stone_chaos]
ID=0ed6
NAME=Resurrection Stone

CATEGORY=Order and Chaos
SUBSECTION=Chaos
DESCRIPTION=Resurrection Stone

ON=@CREATE
   ATTR=010
   COLOR=0af7

ON=@DClick
   IF (0<SRC.TAG.ALIGNMENT>=alignment_chaos)
      IF (<SRC.HITS> == 0)
          SRC.RESURRECT
          CHAOSSAY The ways of chaos flow through you once more...
      ENDIF
   ENDIF
   RETURN 1

[ITEMDEF i_shield_chaos_2]
ID=i_shield_chaos
TYPE=t_shield
FLIP=1
RESOURCES=4 i_ingot_silver, 8 i_ingot_shadow, 2 i_ingot_gold
WEIGHT=1
ARMOR=30
REQSTR=0
SKILLMAKE=BLACKSMITHING 1000, ARMSLORE 1000
CATEGORY=Order and Chaos
SUBSECTION=Chaos
DESCRIPTION=Chaos Shield

ON=@Create
	ATTR=05
	HITPOINTS={101 115}

ON=@EQUIP
   IF ( <SRC.TAG.ALIGNMENT>=alignment_chaos)
      CHAOSSAY You put on your shield and feel the way of chaos flow through you...
      RETURN 0
   ELSE
      UNEQUIP
      SRC.SYSMESSAGE You are not a member of chaos!
      RETURN 1
   ENDIF

[ITEMDEF i_shield_chaos_3]
ID=i_shield_chaos
TYPE=t_shield
NAME=Elite Chaos Shield
FLIP=1
RESOURCES=4 i_ingot_silver, 8 i_ingot_shadow, 2 i_ingot_gold
WEIGHT=1
ARMOR=40
REQSTR=50
SKILLMAKE=BLACKSMITHING 1000, ARMSLORE 1000
CATEGORY=Order and Chaos
SUBSECTION=Chaos
DESCRIPTION=Chaos Shield

ON=@Create
   ATTR=05
   COLOR = 0af7
   HITPOINTS={501 1015}

ON=@EQUIP
   IF <COLOR> = 0
      COLOR = 0af7
   ENDIF

   IF ( <SRC.TAG.ALIGNMENT>=alignment_chaos)
      IF ( <SRC.TAG0.REP.ALIGNMENT> > 8000)
         CHAOSSAY You put on your shield and feel the way of chaos flow through you...
         RETURN 0
      ELSE
         CHAOSSAY Your rank is not high enough to be worthy to wear this shield!
         UNEQUIP
         RETURN 1
      ENDIF
   ELSE
      UNEQUIP
      SRC.SYSMESSAGE You are not a member of chaos!
      RETURN 1
   ENDIF

[ITEMDEF i_war_horse_chaos]
Name=Chaos War Horse
ID=02124
TYPE=T_FIGURINE
TDATA3=c_war_horse_chaos
VALUE=1000000
CATEGORY=Wigifer MPZ
SUBSECTION=Character Icons
DESCRIPTION=Chaos War Horse (tan)

ON=@CREATE
COLOR=0af7
MORE1=c_war_horse_chaos

ON=@DCLICK
if ( (<SRC.TARG.ATTR> & attr_move_never)
      src.sysmessage You Cant Unshrink that
      return 1
ENDIF 

[CHARDEF c_war_Horse_chaos]
DEFNAME=c_war_horse_chaos
ID=c_horse_tan
NAME=Chaos War Horse
SOUND=snd_ANIMALS_HORSE1
ICON=i_pet_horse_tan
ANIM=01fff
CAN=MT_WALK|MT_RUN
RESOURCES=15 i_ribs_raw, 10 i_hide
FOODTYPE=55 t_crops,55 t_fruit,55 t_grain, 64 t_grass
DAM=10,20
ARMOR=20
DESIRES=e_horses,c_man,c_woman
TEVENTS=e_horses
AVERSIONS=e_carnivores3

ON=@Create
COLOR=0af7
NPC=brain_animal
FAME=50
KARMA=8000
STR={1650 1750}
DEX={125 125}
INT={90 100}

PARRYING={85.0 95.0}
MAGICRESISTANCE={195.0 230.0}
TACTICS={90.0 100.0}
WRESTLING={70.0 90.0}
TAMING=100.0
ARMSLORE=100.0

ON=@DCLICK
   COLOR=0af7

DESCRIPTION=Chaos War Horse
SUBSECTION=New Steeds
CATEGORY=Wigifer MPZ


[ITEMDEF i_mount_tub_chaos]
ID	= 0fab
NAME	= mount dying pigment
VALUE	= 75000
ON=@Create
   COLOR	= 0af7

ON=@DClick
   if ( !<SRC.findlayer(layer_horse).uid> )
      src.sysmessage You are not mounted.
      return 1
   endif
   if !( 0<SRC.TAG.ALIGNMENT>=alignment_chaos)
      CHAOSSAY Only members of chaos may use this!
      return 1
   endif
   SRC.ACT	= <SRC.findlayer(layer_horse).uid>
   SRC.ACT.COLOR	= <COLOR>
   SRC.ACT	= <SRC.ACT.MORE2>
   SRC.ACT.COLOR	= <COLOR>
   SRC.ACT.TAG.COLOR	= <COLOR>
   src.act.events +e_dyed
   src.sysmessage You spread the pigment over the <SRC.ACT.NAME>.
   if ( !<SRC.ISGM> )
      REMOVE
   endif
   return 1

[CHARDEF c_chaos_vendor]
NAME=Rei
ID=C_MAN
DESIRES=i_gold,e_notoriety
AVERSIONS=t_TRAP,t_eerie_stuff

TSPEECH=spk_human_prime
TSPEECH=spk_trainer
TSPEECH=spk_needs
TSPEECH=spk_rehello
TSPEECH=spk_human_default
TSPEECH=spk_chaos_vendor

TEVENTS=e_Human_HearUnk
TEVENTS=e_Human_Needs
TEVENTS=e_Human_ConvInit
TEVENTS=e_Human_Greet
TEVENTS=e_Human_Space
TEVENTS=e_Human_Refuse
TEVENTS=e_Human_Environ

ON=@Create
NPC=brain_vendor
COLOR=colors_skin
STR={16 30}
DEX={26 40}
INT={31 45}

EVALUATINGINTEL={45.0 68.0}
WRESTLING={15.0 38.0}
MAGICRESISTANCE={15.0 38.0}
TACTICS={15.0 38.0}
INSCRIPTION={50.0 70.0}

ITEMNEWBIE=random_female_hair
COLOR=colors_hair

ON=@NPCRestock
ITEM=RANDOM_LIGHT
ITEMNEWBIE=i_robe_alignment_chaos
ITEM=random_shoes
COLOR=colors_neutral

SELL=VENDOR_CHAOS_SELL
BUY=VENDOR_CHAOS_BUY

CATEGORY=Wigifer MPZ
SUBSECTION=Chaos
DESCRIPTION=Chaos Vendor

[TEMPLATE VENDOR_CHAOS_SELL]
DEFNAME=VENDOR_CHAOS_SELL
CATEGORY=Wigifer MPZ
SUBSECTION=Templates
DESCRIPTION=Chaos Vendor
SELL=i_robe_alignment_chaos,{1 3}
SELL=i_war_horse_chaos,{1 3}
SELL=i_mount_tub_chaos,{1 3}
SELL=i_potion_refresh,{51 100}
SELL=i_potion_nightsight,{51 100}
SELL=i_potion_heal,{51 100}
SELL=i_potion_cure,{51 100}	
SELL=i_reag_black_pearl,{500 500}
SELL=i_reag_blood_moss,{500 500}
SELL=i_reag_garlic,{500 500}
SELL=i_reag_ginseng,{500 500}
SELL=i_reag_mandrake_root,{500 500}
SELL=i_reag_nightshade,{500 500}
SELL=i_reag_sulfur_ash,{500 500}
SELL=i_reag_spider_silk,{500 500}
SELL=i_xbolt,{250 500}
SELL=i_arrow,{250 500}

[TEMPLATE VENDOR_CHAOS_BUY]
DEFNAME=VENDOR_CHAOS_BUY
CATEGORY=Wigifer MPZ
SUBSECTION=Templates
DESCRIPTION=Chaos Vendor Buy
BUY=i_bandage,{100 150}

[SPEECH spk_chaos_vendor] 
ON=*buy*
   IF (<SRC.FLAGS> & statf_dead)
      SRC.SYSMESSAGE @025 The dead cannot make transactions, friend...
      RETURN 1
   ENDIF
IF (<SRC.TAG.ALIGNMENT>=alignment_chaos)
   SAY It is a pleasure to serve thee...
   BUY
ELSE
   SAY I only serve members of chaos!
   return 1
ENDIF

[EOF]

