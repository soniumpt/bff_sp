//-------------------------Order and Chaos Town Wars-------------------------
//-									    -
//------------------------By James "Wigifer" Timbrell------------------------
//-----------------------And Jessica "Wren" Graham---------------------------
//-									    -
//---------------------------------------------------------------------------


//-----------------------------------NEW TOWNWARS-------------------------------------------
[ITEMDEF i_oc_town_wars]
ID=0ed6
NAME=Town Control Stone
 
ON=@CREATE
   ATTR=08010
   COLOR = 0
  

ON=@CLICK
   //SEE IF THE TERRITORY TAG HAS BEEN SET
   IF (0<TAG.TERRITORY>=0)
      TAG.TERRITORY=<REGION.NAME>
      NAME = <TAG.TERRITORY> Control Stone
   ENDIF

   //DISPLAY THE ALIGNMENT OF THE TERRITORY
   IF (<COLOR>=0)
      SRC.SYSMESSAGE This stone is neutral!
   ELSEIF (<COLOR>=0b03)
      SRC.ORDERSAY This stone belongs to order!
   ELSEIF (<COLOR>=0af7)
      SRC.CHAOSSAY This stone belongs to chaos!
   ENDIF
   

ON=@DCLICK
   //SEE IF THE TERRITORY TAG HAS BEEN SET
   IF (0<TAG.TERRITORY>=0)
      TAG.TERRITORY=<REGION.NAME>
      NAME = <TAG.TERRITORY> Control Stone
   ENDIF

   //CHECK DISTANCE FROM STONE
   IF (<SRC.DISTANCE>>2)
      RETURN 1
   ENDIF   
      
   IF (<SRC.ISALIGNMENT_ORDER>) 
      
      //IF ORDER DOES NOT OWN THE STONE
      IF !(<COLOR>=0b03)
      
         //MESSAGE IF THEY CAN'T CLAIM IT YET
	 IF (<MORE1>=1)
	    SRC.SYSMESSAGE You cannot try to claim this town for another <EVAL (<TIMER>/60/60)> hours.
            RETURN 1
            
         //IF STONE IS IN A STATE TO BE CLAIMED, TRIGGER CLAIM
         ELSEIF (<TIMER>=-1)
            ANNOUNCE ORDER is trying to capture <TAG.TERRITORY>
            SRC.SYSMESSAGE You must remain in <TAG.TERRITORY> to protect it for 30 minutes.
            SRC.EVENTS +OC_Town_Defender
            SRC.TAG.OC_TERRITORY=<UID>
            TAG.DEFENDER01=<SRC.UID>
            TAG.DEFENDERS=1
            TAG.COLOR=<COLOR>
            COLOR=0a17
            TIMER=1800
      
         //IF STONE IS BEING CLAIMED BY ORDER, TRY TO ADD ME AS DEFENDER
         ELSEIF (<COLOR>=0a17)
            ADD_DEFENDERS <SRC.UID>
      
         //IF STONE IS BEING CLAIMED BY CHAOS, TRY TO STOP CLAIM OR ELSE TELL ME WHO IS STILL DEFENDING
         ELSEIF (<COLOR>=0aa8)
            IF (<CHECK_DEFENDERS>= 0)
               ANNOUNCE ORDER has stopped CHAOS from laying claim on <TAG.TERRITORY>
               COLOR=<TAG.COLOR>
               MORE1=1
               TIMER=86400
               SET_REGION_AFFILIATION
               RESET_TOWNWAR
            ELSE
               LIST_DEFENDERS
            ENDIF
         ENDIF
      ENDIF
      
   ELSEIF (<SRC.ISALIGNMENT_CHAOS>) 

      //IF ORDER DOES NOT OWN THE STONE
      IF !(<COLOR>=0af7)
      
         //MESSAGE IF THEY CAN'T CLAIM IT YET
	 IF (<MORE1>=1)
	    SRC.SYSMESSAGE You cannot try to claim this town for another <EVAL (<TIMER>/60)> minutes.
            RETURN 1
            
         //IF STONE IS IN A STATE TO BE CLAIMED, TRIGGER CLAIM
         ELSEIF (<TIMER>=-1)
            ANNOUNCE CHAOS is trying to capture <TAG.TERRITORY>
            SRC.SYSMESSAGE You must remain in <TAG.TERRITORY> to protect it for 30 minutes.
            SRC.EVENTS +OC_Town_Defender
            SRC.TAG.OC_TERRITORY=<UID>
            TAG.DEFENDER01=<SRC.UID>
            TAG.DEFENDERS=1
      	    TAG.COLOR=<COLOR>
            COLOR=0aa8
            TIMER=1800
         
         //IF STONE IS BEING CLAIMED BY ORDER, TRY TO ADD ME AS DEFENDER
         ELSEIF (<COLOR>=0aa8)
            ADD_DEFENDERS <SRC.UID>

         //IF STONE IS BEING CLAIMED BY ORDER, TRY TO STOP CLAIM OR ELSE TELL ME WHO IS STILL DEFENDING
         ELSEIF (<COLOR>=0a17)
            IF (<CHECK_DEFENDERS>= 0)
               ANNOUNCE CHAOS has stopped ORDER from laying claim on <TAG.TERRITORY>
               COLOR=<TAG.COLOR>
               MORE1=1
               TIMER=86400
               SET_REGION_AFFILIATION   
               RESET_TOWNWAR
            ELSE
               LIST_DEFENDERS
            ENDIF
         ENDIF
      ENDIF
      
   ELSE
      SRC.SYSMESSAGE You cannot join Order and Chaos wars!
   ENDIF
   RETURN 1


ON=@TIMER
   //IF DISABLED FROM CAPTURE, ENABLE IT
   IF (<MORE1>=1)
      MORE1=0
      TIMER=-1
      RETURN 1
   ENDIF
   
   //SEE IF ORDER HAS CAPTURED THE STONE
   IF (<COLOR>=0a17)
       IF <CHECK_DEFENDERS>
          ANNOUNCE ORDER has successfully captured <TAG.TERRITORY>
          COLOR=0b03
       ELSE
          COLOR=<TAG.COLOR>
          ANNOUNCE ORDER has failed to capture <TAG.TERRITORY>.       
       ENDIF
       
   //SEE IF CHAOS HAS CAPTURED THE STONE
   ELSEIF (<COLOR>=0aa8)
       IF <CHECK_DEFENDERS>
          ANNOUNCE CHAOS has successfully captured <TAG.TERRITORY>
          COLOR=0af7
       ELSE
          COLOR=<TAG.COLOR>
          ANNOUNCE CHAOS has failed to capture <TAG.TERRITORY>.       
       ENDIF
   ENDIF
   
   //DISABLE CAPTURE FOR 24 HOURS
   SET_REGION_AFFILIATION
   RESET_TOWNWAR
   MORE1=1
   TIMER=86400
   RETURN 1
   

//ADD UP TO 5 DEFENDERS TO THE STONE
[FUNCTION ADD_DEFENDERS]
//No more than 5 official defenders
IF ( 0<TAG.DEFENDERS>=05 )
   MESSAGE No more official defenders can be added.
   RETURN 1
ENDIF

//Check to see if defender is already registered, if not add them.
FOR 1 <EVAL (<TAG.DEFENDERS>+1)>
   IF (0<TAG.DEFENDER<LOCAL._FOR>>=<args>)
      MESSAGE You are already defending this stone.
      RETURN 1   
   ELSEIF (0<TAG.DEFENDER<LOCAL._FOR>>= 0)
      TRY TAG.DEFENDER<LOCAL._FOR>=<args>
      TRY UID.<ARGS>.EVENTS +OC_Town_Defender
      TRY UID.<ARGS>.TAG.OC_TERRITORY=<UID>
      TAG.DEFENDERS=<TAG.DEFENDERS>+1
      RETURN 1
   ENDIF
ENDFOR
RETURN 1


//SEE IF THE STONE HAS BEEN DEFENDED OR NOT
[FUNCTION CHECK_DEFENDERS]
LOCAL.DEFENDERS=<TAG.DEFENDERS>
FOR 1 <TAG.DEFENDERS>
   //Check to see if defenders are logged in, alive, and in this territory.
   IF (0<TAG.DEFENDER<LOCAL._FOR>>=00)
      LOCAL.DEFENDERS=<LOCAL.DEFENDERS>-1
   ELSEIF !(strmatch("<UID.<TAG.DEFENDER<LOCAL._FOR>>.REGION.NAME>","<TAG.TERRITORY>"))
      LOCAL.DEFENDERS=<LOCAL.DEFENDERS>-1
   ELSEIF (<UID.<TAG.DEFENDER<LOCAL._FOR>>.ISONLINE>=0)
      LOCAL.DEFENDERS=<LOCAL.DEFENDERS>-1
   ENDIF
ENDFOR
RETURN <LOCAL.DEFENDERS>


//REMOVE DEFENDER WHEN THEY DIE AND RESET THEIR TAGS AND EVENTS
[FUNCTION DEFENDER_KILLED]
FOR 1 <TAG.DEFENDERS>
   //Change defender's status to killed
   IF strmatch("<TAG.DEFENDER<LOCAL._FOR>>","<args>")
      TRY TAG.DEFENDER<LOCAL._FOR> = 0
   ENDIF
ENDFOR
RETURN 1

[EVENTS OC_Town_Defender]
ON=@Death
   TRY UID.<SRC.TAG.OC_TERRITORY>.DEFENDER_KILLED <SRC.UID>
   SRC.TAG.OC_TERRITORY=
   SRC.EVENTS -OC_Town_Defender


//LIST DEFENDERS TO OPPOSING FACTION WHEN THEY DOUBLE-CLICK THE STONE
[FUNCTION LIST_DEFENDERS]
SRC.SYSMESSAGE The remaining defenders of <TAG.TERRITORY> are:
FOR 1 <TAG.DEFENDERS>
   IF (<TAG.DEFENDER<LOCAL._FOR>> > 0)
      IF (<UID.<TAG.DEFENDER<LOCAL._FOR>>.ISONLINE>=1)
         IF strmatch("<UID.<TAG.DEFENDER<LOCAL._FOR>>.REGION.NAME>","<TAG.TERRITORY>")
            SRC.SYSMESSAGE <UID.<TAG.DEFENDER<LOCAL._FOR>>.NAME> - <UID.<TAG.DEFENDER<LOCAL._FOR>>.P>
         ELSE
            SRC.SYSMESSAGE <UID.<TAG.DEFENDER<LOCAL._FOR>>.NAME> - Outside Territory
         ENDIF
      ELSE
         SRC.SYSMESSAGE <UID.<TAG.DEFENDER<LOCAL._FOR>>.NAME> - Logged Out
      ENDIF
   ENDIF
ENDFOR

OC_UNHIDE

RETURN 1

   
//RESET TAGS AND EVENTS
[FUNCTION RESET_TOWNWAR]
FOR 1 <TAG.DEFENDERS>
   IF (<TAG.DEFENDER<LOCAL._FOR>>>0)
      TRY UID.<TAG.DEFENDER<LOCAL._FOR>>.EVENTS -OC_Town_Defender
      TRY UID.<TAG.DEFENDER<LOCAL._FOR>>.TAG.OC_TERRITORY=
   ENDIF
   TRY TAG.DEFENDER<LOCAL._FOR>=
   TAG.COLOR=
ENDFOR
TAG.DEFENDERS=


//UNHIDE DEFENDERS
[FUNCTION OC_UNHIDE]
FOR 1 <TAG.DEFENDERS>
   IF (<TAG.DEFENDER<LOCAL._FOR>> > 0)
      IF (<UID.<TAG.DEFENDER<LOCAL._FOR>>.ISONLINE>=1)
         IF strmatch("<UID.<TAG.DEFENDER<LOCAL._FOR>>.REGION.NAME>","<TAG.TERRITORY>")
         
            //Cancel Invisibility Spell and Potions
            OBJ=<UID.<TAG.DEFENDER<LOCAL._FOR>>>
            
            IF <OBJ.FINDID.i_rune_invisibility.UID>
               OBJ.FINDID.i_rune_invisibility.REMOVE
            ENDIF
            
            //Unhide people
            IF <eval (<OBJ.flags>&0800000)==0800000> 
               OBJ.SAY 
            ENDIF
         ENDIF
      ENDIF
   ENDIF
ENDFOR
RETURN 1


//SET THE REGION'S OWNER
[FUNCTION SET_REGION_AFFILIATION]
LOCAL.REGION=<STRARG <REGION.NAME>>
LOCAL.APOSTROPHE=<STRPOS 0 39 <LOCAL.REGION>>
IF (<LOCAL.APOSTROPHE> > 0)
   LOCAL.REGION=<STRSUB 0 <LOCAL.APOSTROPHE> <LOCAL.REGION>>
ENDIF

   IF (<COLOR>=0)
      TRY VAR.<STRARG <LOCAL.REGION>>.AFFILIATION=alignment_none
   ELSEIF (<COLOR>=0b03)
      TRY VAR.<STRARG <LOCAL.REGION>>.AFFILIATION=alignment_order
   ELSEIF (<COLOR>=0af7)
      TRY VAR.<STRARG <LOCAL.REGION>>.AFFILIATION=alignment_chaos
   ENDIF
   RETURN 1


//USED BY VENDORS
[FUNCTION GET_REGION_AFFILIATION]
LOCAL.REGION=<STRARG <REGION.NAME>>
LOCAL.APOSTROPHE=<STRPOS 0 39 <LOCAL.REGION>>
IF (<LOCAL.APOSTROPHE> > 0)
   LOCAL.REGION=<STRSUB 0 <LOCAL.APOSTROPHE> <LOCAL.REGION>>
ENDIF

   IF (<EVAL 0<VAR.<LOCAL.REGION>.AFFILIATION>>=0)
      RETURN alignment_none
   ELSEIF strmatch("<VAR.<LOCAL.REGION>.AFFILIATION>","alignment_none")
      RETURN alignment_none
   ELSEIF strmatch("<VAR.<LOCAL.REGION>.AFFILIATION>","alignment_order")
      RETURN alignment_order
   ELSEIF strmatch("<VAR.<LOCAL.REGION>.AFFILIATION>","alignment_chaos")
      RETURN alignment_chaos
   ENDIF


[FUNCTION TESTAFF]
SRC.SAY <GET_REGION_AFFILIATION>

[EOF]   