[CHARDEF c_despise_harriet]
ID=c_woman
NAME=Harriet, the Huntress
DEFNAME=c_despise_harriet
CAN=MT_NONMOVER
CATEGORY=Imperial uo
SUBSECTION=PvM Quests - Despise
DESCRIPTION=Harriet

TSPEECH=spk_despise_harriet

on=@create
FLAGS |= statf_invul
title=the Hunter
str=100
dex=100
int=100
ITEMNEWBIE=i_dress_plain
COLOR=079a
ITEMNEWBIE=i_hair_long
COLOR=0375
ITEMNEWBIE=i_hat_straw_tall
ITEMNEWBIE=i_spellbook


on=@gethit
return 1

on=@spelleffect
return 1


[SPEECH spk_despise_harriet]
ON=*info*
//see if the player has already done the quest (select despise_todd var from quests where uid=src.uid)
serv.log db query
DB.QUERY "SELECT `total`, `despise_harriet` FROM `quests` WHERE `uid` = '<SRC.UID>'"

IF (0<DB.ROW.NUMROWS>)
    FOR CURRENTROW 0 <eval <DB.ROW.NUMROWS>-1>
	//Player has completed the quest if var=10
        IF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==10)
            SAY You have already helped me, I thank thee!
	    RETURN 1
	ENDIF
	//Player has started the quest if var=1
	IF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==01)
            SDIALOG D_DESPISE_HARRIET
	//Player has completed part 1 the quest if var=2
	ELIF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==02)
	    SDIALOG D_DESPISE_HARRIET2
	//Player has started part 2 of the quest if var=3
	ELIF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==03)
            SDIALOG D_DESPISE_HARRIET2
        ELSE
	//Player has not started the quest
            SDIALOG D_DESPISE_HARRIET
        ENDIF
     END
ENDIF


RETURN 1

ON=*accept*
//see if the player has already done the quest (select despise_todd var from quests where uid=src.uid)
serv.log db query
DB.QUERY "SELECT `total`, `despise_todd`, `despise_harriet` FROM `quests` WHERE `uid` = '<SRC.UID>'"

IF (0<DB.ROW.NUMROWS>)
    FOR CURRENTROW 0 <eval <DB.ROW.NUMROWS>-1>
    	//Player has not yet completed Todd quest
    	IF !(<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_todd>==10)
	            SAY Leave me alone, I am busy!
		    RETURN 1
	ENDIF
	//Player has completed the quest if var=10
        IF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==10)
            SAY You have already helped me, I thank thee!
	    RETURN 1
	ENDIF
	//Player has started the quest if var=1
	IF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==1)
            SAY You are already helping me, go and kill those Earth elementals!
	    RETURN 1
	//Player has completed part 1 the quest if var=2
	ELIF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==2)
	serv.log db execute
	    DB.EXECUTE "UPDATE `quests` SET `despise_harriet`='3' WHERE `uid`='<SRC.UID>';"
	    SAY Thank you for helping me, go kill that nasty Elemental!
	    SERV.LOG <SRC.ACCOUNT> - <SRC.NAME>: Has begun Despise, Harriet Quest.
    		 SERV.NEWITEM i_mem_despise3
   		 NEW.TAG.DESPISE2=1
   		 NEW.TIMER = 86400
   		 NEW.EQUIP <SRC.UID>
   		 RETURN 1
	    RETURN 1
	//Player has started part 2 of the quest if var=3
	ELIF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.despise_harriet>==3)
            SAY You are already helping me, go and kill that nasty Elemental!
	    RETURN 1
        ELSE
	//Player has not started the quest
	serv.log db execute
            DB.EXECUTE "UPDATE `quests` SET `despise_harriet`='1' WHERE `uid`='<SRC.UID>';"
	    SAY Thank you for helping me, I am in so much debt to you for this!
	    SERV.LOG <SRC.ACCOUNT> - <SRC.NAME>: Has begun Despise, Harriet Quest.
    		 SERV.NEWITEM i_mem_despise2
   		 NEW.TAG.DESPISE=1
   		 NEW.TIMER = 86400
   		 NEW.EQUIP <SRC.UID>
   		 RETURN 1
        ENDIF
     END
ELSE
    SAY You do not look worthy enough to save me, leave me alone!
ENDIF


ON=*reward*
IF !(<SRC.FINDID(i_mem_despise2)>)
    SAY Reward for what? You've not done anything for me yet.
    RETURN 1
ENDIF
IF (<SRC.FINDID(i_mem_despise2).tag.despise> == 11)
SERV.LOG <SRC.ACCOUNT> - <SRC.NAME>: Has been rewarded for  Despise, Harriet Quest - part 1.
serv.log db query
DB.QUERY "SELECT `total` FROM `quests` WHERE `uid` = '<SRC.UID>'"
IF (0<DB.ROW.NUMROWS>)
    FOR CURRENTROW 0 <eval <DB.ROW.NUMROWS>-1>
    IF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.total>==0)
    LOCAL.QTOTAL=1
    ELSE
    LOCAL.QTOTAL=<eval <DB.ROW.<eval <LOCAL.CURRENTROW>>.total>+1>
    ENDIF
    END
ENDIF
serv.log db execute
DB.EXECUTE "UPDATE `quests` SET `total`='<EVAL <LOCAL.QTOTAL>>', `despise_harriet`='2' WHERE `uid`='<SRC.UID>'"
LOCAL.QTOTAL=
SAY Thank you! Here is your reward
    SRC.FINDID(i_mem_despise2).REMOVE
    SERV.NEWITEM i_gold
    SRC.EVENTS -e_despise_harriet
    NEW.AMOUNT 10000
    NEW.BOUNCE
RETURN 1
ELIF (<SRC.FINDID(i_mem_despise2).tag.despise> >= 1)
SAY You have not collected me all I require yet. You need <eval 11 - <SRC.FINDID(i_mem_despise2).tag.despise>> more elemetal spirits.
RETURN 1
ENDIF

IF (<SRC.FINDID(i_mem_despise3).tag.despise2> == 2)
serv.log db query
DB.QUERY "SELECT `total` FROM `quests` WHERE `uid` = '<SRC.UID>'"
SERV.LOG <SRC.ACCOUNT> - <SRC.NAME>: Has completed Despise, Harriet Quest - part 2.
IF (0<DB.ROW.NUMROWS>)
    FOR CURRENTROW 0 <eval <DB.ROW.NUMROWS>-1>
    IF (<DB.ROW.<eval <LOCAL.CURRENTROW>>.total>==0)
    SRC.TAG.QTOTAL=1
    ELSE
    SRC.TAG.QTOTAL=<eval <DB.ROW.<eval <LOCAL.CURRENTROW>>.total>+1>
    ENDIF
    END
ENDIF
serv.log db execute
DB.EXECUTE "UPDATE `quests` SET `total`='<EVAL <SRC.TAG.QTOTAL>>', `despise_harriet`='10' WHERE `uid`='<SRC.UID>'"
SRC.TAG.QTOTAL=
SAY Thank you! Here is your reward
    SRC.FINDID(i_mem_despise3).REMOVE
    SRC.EVENTS -e_despise_harriet2
    SERV.NEWITEM i_elemental_spirit
    NEW.AMOUNT 1
    NEW.BOUNCE
RETURN 1
ELSE
SAY You have not yet killed the Elemental. How will I ever get past him?
RETURN 1
ENDIF



[EVENTS e_despise_harriet]
ON=@DeathCorpse
IF (<ACT.FINDID(i_mem_despise2).tag.despise> == 10)
ENDIF
IF (<ACT.FINDID(i_mem_despise2).tag.despise> >= 01)
   	IF (rand(100) < 50)
   	SERV.NEWITEM i_elemental_spirit_npc, 1, <ARGO.UID>
   	ENDIF
ENDIF


[EVENTS e_despise_harriet2]
ON=@Death
IF (<ACT.FINDID(i_mem_despise3).tag.despise2> == 02)
ENDIF
IF (<ACT.FINDID(i_mem_despise3).tag.despise2> == 01)
   ACT.FINDID(i_mem_despise3).tag.despise2=<eval <ACT.FINDID(i_mem_despise3).tag.despise2> + 1>
   SRC.SYSMESSAGE @35 You killed the Nuclear Elemental - Go see Harriet for your reward.
ENDIF


[DIALOG D_DESPISE_HARRIET]
200,180
PAGE 1
resizepic 20 20 3500 350 170
checkertrans 20 20 350 170
dhtmlgump 60 40 280 120 1 1 <DEF.BFONT_DGREEN>Oh, hello there. You made me jump! I came here with Todd, you may have met him around here somewhere.<DEF.BR> Anyway, I have been looking for some samples of Earth elementals to take back to our master with Todd.<DEF.BR> Could you help me get some? I am not strong enough to kill them myself.<DEF.BFONTE>

[DIALOG D_DESPISE_HARRIET2]
200,180
PAGE 1
resizepic 20 20 3500 350 170
checkertrans 20 20 350 170
dhtmlgump 60 40 280 120 1 1 <DEF.BFONT_DGREEN>Thanks for your help, these spirits of elementals are just what I need!<DEF.BR> Could I however, ask for your help again? You see that Nuclear Elemental not too far away? His radioactivity is harming me and I am scared to go back past him in case it causes me severe damage. <DEF.BR>If you could kill him for me, I can let you have one of these samples you got me.<DEF.BFONTE>


[ITEMDEF i_mem_despise2]
ID=i_memory
TYPE=t_eq_script
NAME=Depsise quest 2

on=@Timer
serv.log db execute
    DB.EXECUTE "UPDATE `quests` SET `despise_harriet`='0' WHERE `uid`='<CONT.UID>';"
    REMOVE
    CONT.EVENTS -e_despise_harriet
    RETURN 1
    

[ITEMDEF i_mem_despise3]
ID=i_memory
TYPE=t_eq_script
NAME=Depsise quest 3

on=@Timer
serv.log db execute
    DB.EXECUTE "UPDATE `quests` SET `despise_harriet`='2' WHERE `uid`='<CONT.UID>';"
    REMOVE
    CONT.EVENTS -e_despise_harriet2
    RETURN 1


[ITEMDEF i_elemental_spirit_npc]
ID=i_reag_pumice
TYPE=t_script
NAME=Spirit of an Elemental

ON=@Create
COLOR=0158


ON=@dclick
IF !(<SRC.FINDID(i_mem_despise2).tag.despise>)
SRC.EMOTE chuck away the useless item.
REMOVE
RETURN 1
ENDIF
IF (<SRC.FINDID(i_mem_despise2).tag.despise>==11)
SRC.SYSMESSAGE @35 You already have enough of these. Go and speak to Harriet.
REMOVE
RETURN 1
ELSE
SRC.FINDID(i_mem_despise2).tag.despise=<eval <SRC.FINDID(i_mem_despise2).tag.despise> + 1>
SRC.SYSMESSAGE @35 You found an Elemental Spirit You now have <eval <SRC.FINDID(i_mem_despise2).tag.despise> - 1>
REMOVE
RETURN 1
ENDIF

[ITEMDEF i_elemental_spirit]
ID=i_reag_pumice
TYPE=T_NORMAL
NAME=Spirit of an Elemental

ON=@Create
COLOR=0158
[eof]