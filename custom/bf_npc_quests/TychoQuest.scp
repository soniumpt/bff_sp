//Quest by Karl Ironblade

[CHARDEF c_h_Tycho]
   Name=Tycho
   ID=c_man
   DAM={10,15}
   DESIRES=i_gold,e_notoriety
   AVERSIONS=t_TRAP,t_eerie_stuff
   TSPEECH=spk_human_default
   TSPEECH=spk_quest_tycho

ON=@NPCSeeNewPlayer
	SAY Ah, hello brave adventurer, if you have the time to assist me,
	SAY then I need your help! If you wish to assist then say aye.

ON=@Create
	NPC=brain_human
	COLOR=colors_skin
	STR={66 80}
	DEX={66 80}
	INT={61 75}

	FENCING=80.0
	PARRYING=100.0
	SWORDSMANSHIP=100.0
	WRESTLING=80.0
	MACEFIGHTING=75.0
	TACTICS=95.0

	ITEMNEWBIE=random_male_hair
	COLOR=colors_hair
	ITEMNEWBIE=random_facial_hair
	COLOR=match_hair
	ITEMNEWBIE=i_shield_buckler_steel
	ITEMNEWBIE=i_chainmail_coif_steel
	ITEMNEWBIE=i_chainmail_leggings_steel
        ITEMNEWBIE=i_chainmail_tunic_steel
	ITEMNEWBIE=random_shoes
	COLOR=colors_neutral
	ITEMNEWBIE=i_lantern
	ITEM=random_coin_purse

ON=@NPCRestock
	ITEMNEWBIE=i_shield_buckler_steel
	ITEMNEWBIE=i_chainmail_coif_steel
	COLOR=colors_all
	ITEMNEWBIE=i_chainmail_leggings_steel
	COLOR=colors_all
        ITEMNEWBIE=i_chainmail_tunic_steel
	ITEMNEWBIE=random_shoes
	COLOR=colors_neutral
	ITEMNEWBIE=i_lantern
	ITEM=random_coin_purse

CATEGORY=Imperial uo
SUBSECTION=Adventurer Sword Quest
DESCRIPTION=Tycho


[SPEECH spk_quest_tycho]
ON=*aye*
ON=*i wish to assist*
ON=*quest*
ON=*help*
	IF ( <SRC.TAG.NPC_QUEST_START> & NPCQ_Tycho )
		SAY Ah, you have already returned <SRC.SEX milord/milady>.
		SAY But I see you have not yet retrieved my sword. Could
		SAY you please find it soon as I fear the creatures are drawing nearer.
	ELSE
		SAY I was adventuring in this dungeon, and had made camp for the night,
		SAY and some fiendish dungeon creature had stolen my sword while I slept.
		SAY I need the sword in order to be able to make my way out of this fiendish
		SAY dungeon. If you can help me, then please tell me you 'agree' to help me.
	ENDIF
	RETURN 1
		
ON=*agree*
	 IF ( <SRC.TAG.NPC_QUEST_START> & NPCQ_Tycho )
		SAY Ah, you have already returned <SRC.SEX milord/milady>.
		SAY But I see you have not yet retrieved my sword. Could
		SAY you please find it soon as I fear the creatures are drawing nearer.
		RETURN 1
	 ELSE
	 	SRC.TAG.NPC_QUEST_START = <eval ( <SRC.TAG.NPC_QUEST_START> | NPCQ_Tycho )>
		SAY Please return my sword to me so I know that the foul creature that
		SAY stole it has been vanquished. I have a vague idea of which creature
		SAY took it, if you wish to know then simply ask 'which'.
		RETURN 0
         ENDIF

ON=*which*           
	IF ( <SRC.TAG.NPC_QUEST_START> & NPCQ_Tycho )
		SAY I fear one of those foul trolls has my sword. They probably think it's some kind
		SAY of toothpick no doubt. The brutes...anyway. There are a considerable number of them
		SAY so be wary, they may not be much on their own but they can hit hard in packs.
	ELSE
		SAY I must know if you will help me before I give you the
		SAY information <SRC.SEX milord/milady>.
	ENDIF
	RETURN 1
	
ON=*reward*
	IF !( <SRC.TAG.NPC_QUEST_START> & NPCQ_Tycho )
		SAY I must know if you will help me before I give you the
		SAY information <SRC.SEX milord/milady>
	ELSEIF (( <SRC.TAG.NPC_QUEST_START> & NPCQ_Tycho ) && !<SRC.RESTEST 1 i_sword_tycho>)
		SAY Hand me my sword, brave adventurer and claim your reward.
	ELSEIF <SRC.RESTEST 1 i_sword_tycho>
		SAY Thank you <SRC.SEX milord/milady>. This sword was passed down from my father, and I 			SAY am glad to have it in my possession again. Here, have some of my bounty from this                 	SAY quest as my reward.
		SRC.CONSUME 1 i_sword_tycho
		SRC.NEWITEM tm_reward_tycho
		SRC.ACT.BOUNCE
		SRC.TAG.NPC_QUEST_START = <eval ( <SRC.TAG.NPC_QUEST_START> &~ NPCQ_Tycho )>
		SRC.TAG.NPC_QUEST_DONE = <eval ( <SRC.TAG.NPC_QUEST_DONE> | NPCQ_Tycho )>
		ANNOUNCE <SRC.NAME> has aided a fellow adventurer in need at Despise dungeon.
		REMOVE
		RETURN 0
   	ENDIF
   	RETURN 1
   	

[ITEMDEF i_sword_tycho]
   ID=i_katana
   NAME=Tycho's Sword
   WEIGHT=8
   CATEGORY=Imperial uo
   SUBSECTION=Dungeon Quest items
   DESCRIPTION=Tycho's Sword

ON = @DClick
   return = <wpn_dclick_bladed>
ON = @Targon_ground
   return = <wpn_targon_ground_bladed>
ON = @Targon_char
   return = <wpn_targon_char_bladed>
ON = @Targon_item
   return = <wpn_targon_item_bladed>


[TEMPLATE tm_reward_tycho]
ITEM=i_gold,{1000}


[CHARDEF c_troll_quest]
DEFNAME=c_troll_quest
ID=c_troll
NAME=Troll
SOUND=snd_MONSTER_TROLL1
ICON=i_pet_TROLL
ANIM=078c7f
DAM=10,23
ARMOR=20
RESOURCES=10 i_ribs_raw
CAN=MT_WALK|MT_USEHANDS

FOODTYPE=15 t_meat_raw
DESIRES=i_gold
AVERSIONS=r_civilization
CATEGORY=Monsters
SUBSECTION=Ettins, Ogres, Trolls, Cyclops, and Titans
DESCRIPTION=Troll (quest)

ON=@Create
   NPC=brain_monster

   STR={175 205}
   DEX={46 65}
   INT={46 70}

   SWORDSMANSHIP={50.0 70.0}
   PARRYING={45.0 60.0}
   MAGICRESISTANCE={45.0 60.0}
   TACTICS={50.0 70.0}
   WRESTLING={50.0 70.0}

   FAME={100 5000}
   KARMA={-3000 -3999}

ON=@NPCRestock
   ITEM=i_sword_tycho

ON=@DEATH
  y_region_loot_death
  y_region_loot_calculator
  loot_TROLL


[EOF]



